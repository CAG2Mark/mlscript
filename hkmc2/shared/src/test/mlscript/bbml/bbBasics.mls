:bbml
//│ Type: ⊤


123
//│ Type: Int

3.14
//│ Type: Num

false
//│ Type: Bool

"bbml"
//│ Type: Str

()
//│ Type: ⊤

fun id(x) = x
id
//│ Type: forall x3_2: x3_2 -> x3_2

fun inc(x) = x + 1
//│ Type: ⊤

fun mul(x, y) = x * y
//│ Type: ⊤

x => x
//│ Type: (x9_1) ->{⊥} x9_1

+
//│ Type: (Int, Int) ->{⊥} Int

(x, y) => x + y
//│ Type: (Int, Int) ->{⊥} Int

((x, y) => x + y)(40, 2)
//│ Type: Int

:e
114 + "514"
//│ ╔══[ERROR] Type error in string literal with expected type Int
//│ ║  l.43: 	114 + "514"
//│ ║        	      ^^^^^
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Int


inc(42)
//│ Type: Int

let x = 42 in x
//│ Type: Int

let x = 1 in let y = 2 in x + y
//│ Type: Int

:e
let x = 0 in x + "1"
//│ ╔══[ERROR] Type error in string literal with expected type Int
//│ ║  l.61: 	let x = 0 in x + "1"
//│ ║        	                 ^^^
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Int

class Foo(x: Int)
//│ Type: ⊤

:e
new Nothingness(0)
//│ ╔══[ERROR] Name not found: Nothingness
//│ ║  l.72: 	new Nothingness(0)
//│ ╙──      	    ^^^^^^^^^^^
//│ ═══[ERROR] Not a valid class: <error>
//│ Type: ⊥

:e
new 42
//│ ╔══[ERROR] Not a valid class: integer literal
//│ ║  l.80: 	new 42
//│ ╙──      	    ^^
//│ Type: ⊥


new Foo(42)
//│ Type: Foo


class Point(x: Num, y: Num, z: Num)
//│ Type: ⊤


new Point(0.0, 0.0, 0.0)
//│ Type: Point

:e
new Foo("1!5!")
//│ ╔══[ERROR] Type error in string literal with expected type Int
//│ ║  l.99: 	new Foo("1!5!")
//│ ║        	        ^^^^^^
//│ ╙── because: cannot constrain  Str  <:  Int
//│ Type: Foo


class Some[A](value: A)
//│ Type: ⊤

new Some(true)
//│ Type: Some[α15_1]
//│ Where:
//│   Bool <: α15_1

new Some(42)
//│ Type: Some[α16_1]
//│ Where:
//│   Int <: α16_1

let p = new Point(1.0, 0.0, 0.0) in p.Point#x
//│ Type: Num

let t = new Some(true) in t.Some#value
//│ Type: Bool

:fixme
:pe
42.Some#value
//│ ╔══[LEXICAL ERROR] Expect at least one digit after the  decimal point
//│ ║  l.128: 	42.Some#value
//│ ╙──       	   ^
//│ ╔══[ERROR] Illegal juxtaposition right-hand side.
//│ ║  l.128: 	42.Some#value
//│ ╙──       	   ^^^^
//│ ╔══[ERROR] Name not found: #
//│ ║  l.128: 	42.Some#value
//│ ╙──       	       ^
//│ ╔══[ERROR] Name not found: value
//│ ║  l.128: 	42.Some#value
//│ ╙──       	        ^^^^^
//│ Type: ⊥


class Printer[T](f: T -> Str)
//│ Type: ⊤

fun foofoo(x) =
  let t = x + 1 in "foo"
//│ Type: ⊤

new Printer(foofoo)
//│ Type: Printer[α24_1]
//│ Where:
//│   α24_1 <: Int

let ip = new Printer(foofoo) in ip.Printer#f(42)
//│ Type: Str

:e
let ip = new Printer(foofoo) in ip.Printer#f("42")
//│ ╔══[ERROR] Type error in string literal with expected type α32_1
//│ ║  l.160: 	let ip = new Printer(foofoo) in ip.Printer#f("42")
//│ ║         	                                             ^^^^
//│ ╟── because: cannot constrain  Str  <:  α32_1
//│ ╟── because: cannot constrain  Str  <:  α32_1
//│ ╟── because: cannot constrain  Str  <:  ¬(~α30_1)
//│ ╟── because: cannot constrain  Str  <:  α30_1
//│ ╙── because: cannot constrain  Str  <:  ¬(~{Int})
//│ Type: Str

class TFun[T](f: T -> T)
//│ Type: ⊤

fun inc(x) = x + 1
//│ Type: ⊤

new TFun(inc)
//│ Type: TFun[α36_1]
//│ Where:
//│   Int <: α36_1
//│   α36_1 <: Int

let tf = new TFun(inc) in tf.TFun#f(1)
//│ Type: Int

:e
let tf = new TFun(inc) in tf.TFun#f("1")
//│ ╔══[ERROR] Type error in string literal with expected type α44_1
//│ ║  l.187: 	let tf = new TFun(inc) in tf.TFun#f("1")
//│ ║         	                                    ^^^
//│ ╟── because: cannot constrain  Str  <:  α44_1
//│ ╟── because: cannot constrain  Str  <:  α44_1
//│ ╟── because: cannot constrain  Str  <:  ¬(~α42_1)
//│ ╟── because: cannot constrain  Str  <:  α42_1
//│ ╙── because: cannot constrain  Str  <:  ¬(~{Int})
//│ Type: Str ∨ Int

class Pair[A, B](fst: A, snd: B)
//│ Type: ⊤

(new Pair(42, true)).Pair#fst
//│ Type: Int

(new Pair(42, true)).Pair#snd
//│ Type: Bool


if 1 < 2 then 1 else 0
//│ Type: Int


if false then 1 else "1"
//│ Type: Int ∨ Str


if 1 is Int then 1 else 0
//│ Type: Int


fun test(x) =
  if x is Int then x + 1 else 0
test
//│ Type: (¬Int ∨ Int) -> Int

test(1)
//│ Type: Int


test("1")
//│ Type: Int


fun fact(n) =
  if n > 1 then n * fact(n - 1) else 1
//│ Type: ⊤

fact
//│ Type: Int -> Int

fact(1)
//│ Type: Int


fun fact2 = case
  0 then 1
  n then n * fact2(n - 1)
//│ Type: ⊤

fact2
//│ Type: Int -> Int

fact2(0)
//│ Type: Int


class Foo[A](x: A -> A)
//│ Type: ⊤

new Foo(x => x)
//│ Type: Foo[α76_1]

fun f(g) = new Foo(g)
f
//│ Type: forall α79_2: (α79_2 -> α79_2) -> Foo[α79_2]

f(x => x).Foo#x
//│ Type: (α81_1) ->{⊥} α81_1

g => (new Foo(g)).Foo#x
//│ Type: (α86_1 -> α86_1) ->{⊥} (α86_1) ->{⊥} α86_1


